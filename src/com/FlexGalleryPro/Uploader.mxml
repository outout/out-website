<?xml version="1.0" encoding="utf-8"?>
<!-- *************************************
 * Flex Gallery Pro
 * http://www.FlepStudio.org         
 * Â© Author: Filippo Lughi           
 * version 1.0                       
 ************************************* -->
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml">
<mx:HTTPService
	id="sending_info" method="POST" resultFormat="text" result="InfoSent(event)" useProxy="false">
	<mx:request>
		<name>{T_name.text}</name>
   		<des>{T_des.text}</des>
   		<img>{my_label.text}</img>
    </mx:request>
</mx:HTTPService>
	<mx:Panel id="panel_upload" title="Upload Picture" paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10">
	<mx:Label id="my_label" styleName="myBigBoldStyle"></mx:Label>
	<mx:Label id="F_name" text="Name:" styleName="myBoldStyle"></mx:Label>
	<mx:TextArea id="T_name" width="360" height="20" styleName="myPlainStyle"></mx:TextArea>
	<mx:Label id="F_des" text="Description:" styleName="myBoldStyle"></mx:Label>
	<mx:TextArea id="T_des" width="360" height="100" styleName="myPlainStyle"></mx:TextArea>
    <mx:ProgressBar id="uploadProgress" label="" mode="manual"/>
    <mx:ControlBar horizontalAlign="right">
        <mx:Button id="start_upload" label="Browse..." styleName="myBoldStyle"/>
        <mx:Button id="cancel_upload" label="Cancel" click="cancelUpload();" styleName="myBoldStyle"/>
    </mx:ControlBar>
</mx:Panel>
<mx:Script>
		<![CDATA[
			import mx.rpc.events.ResultEvent;
			import mx.managers.PopUpManager;
			import caurina.transitions.Tweener;
			
			private const UPLOAD_URL:String=FlexGalleryPro._path+"/PHP/upload.php";
			private var fr:FileReference;
			private var pb:ProgressBar;
			private var btn:Button;
			private var fileName:String;
			private var my_parent:*;

			public function positionMe(m:*):void
			{
				my_parent=m;
				
				reset();
				
				panel_upload.width=400;
				panel_upload.height=440;
				panel_upload.x=stage.stageWidth/2-panel_upload.width/2;
				panel_upload.y=my_parent.bar.height+10;
				
				start_upload.addEventListener(MouseEvent.CLICK,startUpload);
				
				init(uploadProgress,start_upload);
			}
			
			public function init(pb:ProgressBar,btn:Button):void
			{	
			    this.pb=pb;
			    this.btn=btn;
			    this.pb.visible=false;
			
			    fr=new FileReference();
			    fr.addEventListener(Event.SELECT,selectHandler);
			    fr.addEventListener(Event.OPEN,openHandler);
			    fr.addEventListener(ProgressEvent.PROGRESS,progressHandler);
			    fr.addEventListener(Event.COMPLETE,completeHandler);
			}
			
			private function startUpload(event:MouseEvent):void
			{
				var imageTypes:FileFilter=new FileFilter("Images (*.jpg, *.gif, *.png)", "*.jpg; *.gif; *.png");
				var allTypes:Array=new Array(imageTypes);
				fr.browse(allTypes);
			}
			
			private function cancelUpload():void
			{
				my_parent.removeChild(this);
				my_parent.add_button.enabled=true;
				backFromReset();
			}
			
			private function selectHandler(event:Event):void
			{	
				start_upload.removeEventListener(MouseEvent.CLICK,startUpload);
				this.pb.visible=true;
				this.btn.enabled=false;
				cancel_upload.enabled=false;
				
				my_label.text="images/"+fr.name;
				fileName=fr.name;
				sendInfo();
				
				T_name.selectable=false;
				T_name.setStyle("borderStyle","none");
				T_des.selectable=false;
				T_des.setStyle("borderStyle","none");
				
			    var request:URLRequest=new URLRequest();
			    request.url=UPLOAD_URL;
			    fr.upload(request);
			}
			
			private function openHandler(event:Event):void
			{
				
			}
			
			private function progressHandler(event:ProgressEvent):void
			{
				pb.setProgress(event.bytesLoaded,event.bytesTotal);
			}
			
			private function completeHandler(event:Event):void
			{
				cancel_upload.visible=false;
				my_label.text='done';
				pb.setProgress(0,0);
				this.pb.visible=false;
				
				this.btn.enabled=true;
				start_upload.label="Close";
				start_upload.addEventListener(MouseEvent.CLICK,closeThis);
			}
			
			private function sendInfo():void
			{
				sending_info.url=FlexGalleryPro._path+"/PHP/addImage.php";
				sending_info.send();
			}
			
			private function InfoSent(Event:ResultEvent):void
			{
				
			}
			
			private function closeThis(event:MouseEvent):void
			{
				my_parent.removeChild(this);
				my_parent.add_button.enabled=true;
				removePopUps();
			}
			
			private function reset():void
            {
            	for(var i:int=0;i<my_parent.win_array.length;i++)
            	{
            		my_parent.win_array[i].visible=false;
            	}
            }
            
            private function backFromReset():void
            {
            	for(var i:int=0;i<my_parent.win_array.length;i++)
            	{
            		my_parent.win_array[i].visible=true;
            	}
            }
            
            private function removePopUps():void
            {
            	for(var i:int=0;i<my_parent.win_array.length;i++)
            	{
            		PopUpManager.removePopUp(my_parent.win_array[i]);
            	}
            	my_parent.win_array=new Array();
            	FlexGalleryPro.obj_array=new Array();
            	my_parent.displayCMS();
            }
		]]>
	</mx:Script>
</mx:Canvas>